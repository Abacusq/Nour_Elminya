;//TF-341 Artificial Intelligence Script
;//Script by Errant AI, Requires FPSC x9 v1.18 Beta 6 or later and character with TF341 animations.

desc          = DarkAI TF341 shoot and melee default full-auto gun - Assign to Shoot AI slot must be used with matching Main AI script
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Walking animations shoot
:isdarkai=1,aicanshoot=1,ducking=0,strafingleft=1:animationnormal,animate=3
:isdarkai=1,aicanshoot=1,ducking=0,strafingright=1:animationnormal,animate=4
:isdarkai=1,aicanshoot=1,ducking=0,movingforwards=1:animationnormal,animate=2
:isdarkai=1,aicanshoot=1,ducking=0,runningforwards=1:animationnormal,animate=5
:isdarkai=1,aicanshoot=1,ducking=0,movingbackwards=1:animationreverse,animate=2
:isdarkai=1,aicanshoot=1,ducking=0,idle=1:animationnormal,animate=26
;//Crouching Animations-shoot
:isdarkai=1,aicanshoot=1,ducking=1,strafingleft=1:animationnormal,animate=32
:isdarkai=1,aicanshoot=1,ducking=1,strafingright=1:animationnormal,animate=32
:isdarkai=1,aicanshoot=1,ducking=1,movingforwards=1:animationnormal,animate=32
:isdarkai=1,aicanshoot=1,ducking=1,movingbackwards=1:animationreverse,animate=32
:isdarkai=1,aicanshoot=1,ducking=1,idle=1:animationnormal,animate=31
;//Walking animations-no shoot
:isdarkai=1,aicanshoot=0,ducking=0,strafingleft=1:animationnormal,animate=34
:isdarkai=1,aicanshoot=0,ducking=0,strafingright=1:animationnormal,animate=34
:isdarkai=1,aicanshoot=0,ducking=0,movingforwards=1:animationnormal,animate=34
:isdarkai=1,aicanshoot=0,ducking=0,runningforwards=1:animationnormal,animate=40
:isdarkai=1,aicanshoot=0,ducking=0,movingbackwards=1:animationreverse,animate=34
:isdarkai=1,aicanshoot=0,ducking=0,idle=1:animationnormal,animate=1
;//Crouching Animations-no shoot
:isdarkai=1,aicanshoot=0,ducking=1,strafingleft=1:animationnormal,animate=34
:isdarkai=1,aicanshoot=0,ducking=1,strafingright=1:animationnormal,animate=34
:isdarkai=1,aicanshoot=0,ducking=1,movingforwards=1:animationnormal,animate=34
:isdarkai=1,aicanshoot=0,ducking=1,movingbackwards=1:animationreverse,animate=34
:isdarkai=1,aicanshoot=0,ducking=1,idle=1:animationnormal,animate=33
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//TARGETING
:state=1,aihastarget=0,aicanshoot=1:aisettarget,airotatetotarget
:aihastarget=1,aicanshoot=0:aicleartarget,aisettarget,airotatetotarget
:state=1:airotatetotarget
:aiaction=0:aiusefullaim=0
:aiaction=1:aiusefullaim=1
:aiaction=2:aiusefullaim=0
:aiaction=3:aiusefullaim=1
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Respond to any sounds while not in combat
:state=1,idle=1,aicanshoot=0,aiheardsound=1000:aimovetosound
:state=1,idle=0,aicanshoot=0,aiheardsound=500:aimovetosound
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Assist allies if called while inactive
:isdarkai=1,aicalled=2000,aihastarget=0,idle=1:airespondtocall
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Fall back to cover
:state=60:airotatetotarget,etimerstart,aimovetocover=0,state=61,setvar=SEAY $EAY,addvar=SEAY 180
:state=61:aisettarget
:state=61:rotatetotarget
:state=61:setvar=SEAY $EAY,airotatey=%SEAY
:state=61,aiatcover=1:state=2
:state=61,etimergreater=1000,aitargetdistwithin=60,rateoffire:state=190
:state=61,etimergreater=3000:state=2
;//Get super defensive when low health to avoid DAI taking over and turning AI's back to hostiles
:state=1,isdarkai=1,healthless=150,idle=0,random=10:state=60
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//close with enemy  for melee when seen nearby
:state=1,aicanshoot=1,aihastarget=1,aitargetdistwithin=1000,random=5:aistop,etimerstart,aisettarget,airotatetotarget,state=55,aicallteam=1000,aisetspeed=150
:state=55:aisettarget,aimovetotarget
:state=55,aitargetdistwithin=60:state=190
:state=55,aitargetdistfurther=1050:state=2
:state=55,aicanshoot=0:state=2
:state=55,aihastarget=0:state=2
:state=55,etimergreater=5000:state=2
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Enemy at very close range
:state=1,aitargetdistwithin=60,varequal=beenshot 0:state=10
:state=1,aitargetdistwithin=60,varequal=beenshot 0,random=5:state=60
:state=10,ifweapon=1,random=1:airotatetotarget,state=190
:state=10,ifweapon=0:airotatetotarget,state=190
:state=10,aitargetdistfurther=80:state=2
;//melee strike - random hand or foot strike
:state=190:state=191,etimerstart
:state=191:airotatetotarget
:state=191,aitargetdistwithin=70,aicanshoot=1:state=192,aistop
:state=191,aitargetdistfurther=70:state=2
:state=191,aicanshoot=0:state=2
;//Fists or MeleeWeap
:state=192,hasweapon=1,random=10:setaiactive=0,state=793,setframe=114
:state=192,random=5:setaiactive=0,state=293,setframe=23
:state=192,random=3:setaiactive=0,state=393,setframe=9
:state=192,hasweapon=1,random=2:setaiactive=0,state=693,setframe=111
:state=192,hasweapon=1,random=1:setaiactive=0,state=593,setframe=110
:state=192,hasweapon=0,random=1:setaiactive=0,state=193,setframe=8
:state=192:setaiactive=0,state=493,setframe=27
;//Strike Punch
:state=193:incframe=8,airotatetotarget
:state=193,framebeyond=8 40,aitargetdistwithin=70:state=194,aisetmeleedamage=10,aiusemelee=1,sound=audiobank\misc\melee.wav
:state=193,frameatend=8:state=195
:state=194:incframe=8,airotatetotarget
:state=194,frameatend=8:state=195
:state=194,etimergreater=1500:state=195
;//Strike 2 Uppercut
:state=393:incframe=9,airotatetotarget
:state=393,framebeyond=9 40,aitargetdistwithin=70:state=394,aisetmeleedamage=10,aiusemelee=1,sound=audiobank\misc\melee.wav
:state=393,frameatend=9:state=195
:state=394:incframe=9,airotatetotarget
:state=394,frameatend=9:state=195
:state=394,etimergreater=1500:state=195
;//Strike 3 Lefthand
:state=493:incframe=27,airotatetotarget
:state=493,framebeyond=27 40,aitargetdistwithin=70:state=494,aisetmeleedamage=10,aiusemelee=1,sound=audiobank\misc\melee.wav
:state=493,frameatend=27:state=195
:state=494:incframe=27,airotatetotarget
:state=494,frameatend=27:state=195
:state=494,etimergreater=1500:state=195
;//Strike 4 Slash
:state=593:incframe=110,airotatetotarget
:state=593,framebeyond=110 30,aitargetdistwithin=80:state=594,useweapon,sound=audiobank\misc\melee_kick.wav
:state=593,frameatend=110:state=195
:state=594:incframe=110,airotatetotarget
:state=594,frameatend=110:state=195
:state=594,etimergreater=1500:state=195
;//Strike 5 Thrust
:state=693:incframe=111,airotatetotarget
:state=693,framebeyond=111 30,aitargetdistwithin=80:state=694,useweapon,sound=audiobank\misc\melee_kick.wav
:state=693,frameatend=111:state=195
:state=694:incframe=111,airotatetotarget
:state=694,frameatend=111:state=195
:state=694,etimergreater=1500:state=195
;//Strike 6 Chop
:state=793:incframe=114,airotatetotarget
:state=793,framebeyond=114 30,aitargetdistwithin=80:state=794,useweapon,sound=audiobank\misc\melee_kick.wav
:state=793,frameatend=114:state=195
:state=794:incframe=114,airotatetotarget
:state=794,frameatend=114:state=195
:state=794,etimergreater=1500:state=195
;//Strike Kick
:state=293:incframe=23,airotatetotarget
:state=293,framebeyond=23 40,aitargetdistwithin=70:state=294,aisetmeleedamage=10,aiusemelee=1,sound=audiobank\341\melee_kick.wav
:state=293,frameatend=23:state=195
:state=294:incframe=23,airotatetotarget
:state=294,frameatend=23:state=195
:state=294,etimergreater=1500:state=195
:state=195,random=1:etimerstart,setaiactive=1,aiattackawareness=0,aicleartarget,state=196,aimoverandom,setvar=SEAY $EAY,addvar=SEAY 180,aisetspeed=200
:state=196:setvar=SEAY $EAY,airotatey=%SEAY
:state=196:aisettarget
:state=196:rotatetotarget
:state=196,etimergreater=1000,random=3:aistop,state=2,aiattackawareness=1,aisettarget,airotatetotarget
:state=196,etimergreater=500,idle=1:aistop,state=2,aiattackawareness=1,aisettarget,airotatetotarget
:state=195:setaiactive=1,aistop,state=2

;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Shot Damage - Flinch (random)  left / right / gut - take cover
:shotdamage=1,varequal=beenshot 0:setvar=beenshot 1,aicallteam=1500
:shotdamage=1,varnotequal=beenshot 0:sound=audiobank\eai\melee_i_soft.wav
;//Left Flinch
:varequal=beenshot 1,shotdamagetype=1,random=1:setvar=beenshot 2,aistop,setaiactive=0,state=123,setframe=44,sound=audiobank\voices\male\land.wav
:varequal=beenshot 1,shotdamagetype=3:setvar=beenshot 2,aistop,setaiactive=0,state=121,setframe=45,sound=audiobank\eai\melee_i_soft.wav
;//:varequal=beenshot 1,shotdamagetype=2,random=6:setvar=beenshot 2,aistop,setaiactive=0,state=121,setframe=45,sound=audiobank\eai\melee_i_soft.wav
:state=121:incframe=45,freeze
:state=121,framebeyond=45 80:state=124
;//Right Flinch
:varequal=beenshot 1,shotdamagetype=1:setvar=beenshot 2,aistop,setaiactive=0,state=122,setframe=46,sound=audiobank\eai\melee_i_soft.wav
:varequal=beenshot 1,shotdamagetype=4:setvar=beenshot 2,aistop,setaiactive=0,state=122,setframe=46,sound=audiobank\eai\melee_i_soft.wav
;//:varequal=beenshot 1,shotdamagetype=2,random=5:setvar=beenshot 2,aistop,setaiactive=0,state=122,setframe=46,sound=audiobank\eai\melee_i_soft.wav
:state=122:incframe=46,freeze
:state=122,framebeyond=46 80:state=124
;//Gut Flinch
:varequal=beenshot 1,shotdamagetype=5:setvar=beenshot 2,aistop,setaiactive=0,state=123,setframe=44,sound=audiobank\voices\male\land.wav
:varequal=beenshot 1,shotdamagetype=6:setvar=beenshot 2,aistop,setaiactive=0,state=123,setframe=44,sound=audiobank\voices\male\land.wav
;//:varequal=beenshot 1,shotdamagetype=2,random=4:setvar=beenshot 2,aistop,setaiactive=0,state=123,setframe=44,sound=audiobank\voices\male\land.wav
:state=123:incframe=44,freeze
:state=123,framebeyond=44 80:state=124
;//Ignore
:varequal=beenshot 1,shotdamagetype=2:beenshot 0
;//Go to cover or panic
:state=124,hasweapon=1,ifweapon=1,idle=1,random=20:setaiactive=1,state=125
:state=124,hasweapon=1,ifweapon=1,idle=1,random=5:setaiactive=1,state=60,aisettarget,setvar=beenshot 0
:state=124,hasweapon=1,ifweapon=1,idle=0,random=10:setaiactive=1,state=60,aisettarget,setvar=beenshot 0
:state=124,hasweapon=1,ifweapon=0,idle=1,random=2:setaiactive=1,state=60,aisettarget,setvar=beenshot 0
:state=124,hasweapon=1,ifweapon=0,idle=0,random=5:setaiactive=1,state=60,aisettarget,setvar=beenshot 0
:state=124,hasweapon=0,random=5:setaiactive=1,state=60,aisettarget,setvar=beenshot 0
:state=124,hasweapon=0:setaiactive=1,state=125
;//Cower
:state=125:setaiactive=0,setframe=131,state=130,etimerstart
:state=130:incframe=131,freeze
:state=130,frameatend=131:setframe=132,state=132,setvar=beenshot 3
:state=132:aicallteam=2000
:state=132:incframe=132
:state=132,frameatend=132:setframe=132,state=132
:state=132,etimergreater=300,random=50:setframe=134,state=133,setvar=beenshot 5
:state=133:incframe=134,freeze
:state=133,frameatend=134:setvar=beenshot 999
:state=134:incframe=133,freeze
:state=134,frameatend=133:setframe=132,state=132,setvar=beenshot 3
:varequal=beenshot 999:setaiactive=1,setvar=beenshot 0,state=1
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Stand down after inaction delay
:state=1,idle=1,aicanshoot=0,etimergreater=8000:state=999
:state=999:setvar=beenshot 99,state=1000
:state=1000:aistop,setaiactive=0,setframe=25,state=1001
:state=1001,healthless=170:addhealth=1
:state=1001:incframe=25,freeze
:state=1001,frameatend=25:setaiactive=1,aistop,state=1002
:state=1002:etimerstart,setvar=beenshot 0,aicleartarget,runfpidefault=1
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Open Doors
:nearactivatable=0:settarget,activatetarget=2
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Headshot
::headshot=1,headshotdamage=100
;------------------------------------------------------------------------------------------------------------------------------------------------------
;//Start Script
:state=0:setaiactive=1,alwaysactive=1,aiattackawareness=1,state=2
:state=2:state=1,etimerstart,aisetspeed=100
;//End